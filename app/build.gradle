apply plugin: 'com.android.application'
apply plugin: 'kotlin-android'
apply plugin: 'kotlin-kapt'
apply plugin: 'kotlin-android-extensions'
apply plugin: 'org.sonarqube'
// TODO: Uncomment this to enable FirebaseAnalytics and Crashlytics
//apply plugin: 'io.fabric'

static def getKey(String env, String key) {
    Properties props = new Properties()
    props.load(new FileInputStream(new File(env.concat('.properties'))))
    return props[key]
}

androidExtensions {
    experimental true
}

android {
    compileSdkVersion 29

    defaultConfig {
        applicationId "ro.code4.monitorizarevot"
        minSdkVersion 21
        targetSdkVersion 29
        versionCode 1
        versionName "1.0"
        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"


        javaCompileOptions {
            annotationProcessorOptions {
                arguments = [
                        "room.schemaLocation"  : "$projectDir/schemas".toString(),
                        "room.incremental"     : "true",
                        "room.expandProjection": "true"]
            }
        }
        vectorDrawables.useSupportLibrary = true
    }

    sourceSets {
        androidTest.assets.srcDirs += files("$projectDir/schemas".toString())
    }

    signingConfigs {
        fastlane {
            storeFile file(getKey("keystore", "storeFile"))
            keyAlias getKey("keystore", "keyAlias")
            keyPassword getKey("keystore", "keyPassword")
            storePassword getKey("keystore", "storePassword")
        }

    }

    buildTypes {
        applicationVariants.all { variant ->
            variant.buildConfigField "String", "API_URL", "\"" + getKey(variant.buildType.name, "apiUrl") + "\""
            variant.buildConfigField "String", "GUIDE_URL", "\"" + getKey(variant.buildType.name, "guideUrl") + "\""
            variant.buildConfigField "String", "ORGANISATION_WEB_URL", "\"" + getKey(variant.buildType.name, "organisationWebUrl") + "\""
            variant.buildConfigField "String", "SERVICE_CENTER_PHONE_NUMBER", "\"" + getKey(variant.buildType.name, "serviceCenterPhoneNumber") + "\""
            variant.buildConfigField "String", "PREFERRED_LOCALE", "\"" + getKey(variant.buildType.name, "preferredLocale") + "\""
            variant.buildConfigField "String", "PRIVACY_WEB_URL", "\"" + getKey(variant.buildType.name, "privacyWebUrl") + "\""
            variant.buildConfigField "String", "SUPPORT_EMAIL", "\"" + getKey(variant.buildType.name, "supportEmail") + "\""

            def productFlavor = variant.productFlavors[0] != null ? "${variant.productFlavors[0].name.capitalize()}" : ""
            def buildType = "${variant.buildType.name.capitalize()}"
            tasks["compile${productFlavor}${buildType}UnitTestSources"].dependsOn(tasks["merge${productFlavor}${buildType}Assets"])
        }

        debug {
            minifyEnabled false
            manifestPlaceholders = [enableCrashReporting:"false"]
        }

        release {
            minifyEnabled false
            signingConfig signingConfigs.fastlane
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
            manifestPlaceholders = [enableCrashReporting:"true"]
        }
    }

    lintOptions {
        baseline file("lint-baseline.xml")
        warningsAsErrors true
        abortOnError true
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }
    buildToolsVersion = '29.0.2'

    kotlinOptions {
        jvmTarget = "1.8"
    }
}

ext {
    retrofitLibraryVersion = '2.5.0'
    koinVersion = '2.0.1'
    roomVersion = '2.2.0'
    firebaseVersion = '17.2.1'
    crashlyticsVersion = '2.10.1'
    adapterDelegatesVersion = "4.2.0"
}

dependencies {
    implementation fileTree(dir: 'libs', include: ['*.jar'])
    implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk7:$kotlin_version"
    implementation 'androidx.appcompat:appcompat:1.1.0'
    implementation "androidx.preference:preference-ktx:1.1.0"
    implementation 'androidx.core:core-ktx:1.1.0'
    implementation 'androidx.legacy:legacy-support-v4:1.0.0'
    implementation 'com.google.android.material:material:1.2.0-alpha01'
    implementation 'androidx.constraintlayout:constraintlayout:1.1.3'
    implementation 'androidx.navigation:navigation-fragment:2.1.0'
    implementation 'androidx.navigation:navigation-ui:2.1.0'
    implementation 'androidx.lifecycle:lifecycle-extensions:2.1.0'
    implementation 'androidx.navigation:navigation-fragment-ktx:2.1.0'
    implementation 'androidx.navigation:navigation-ui-ktx:2.1.0'
    implementation 'androidx.annotation:annotation:1.1.0'

    implementation 'io.reactivex.rxjava2:rxandroid:2.1.1'

    implementation "com.squareup.retrofit2:retrofit:$retrofitLibraryVersion"
    implementation "com.squareup.retrofit2:converter-gson:$retrofitLibraryVersion"
    implementation "com.squareup.retrofit2:adapter-rxjava2:$retrofitLibraryVersion"
    implementation "com.squareup.retrofit2:converter-scalars:$retrofitLibraryVersion"
    implementation 'com.squareup.okhttp3:logging-interceptor:4.1.1'

    // Firebase
    implementation "com.google.firebase:firebase-analytics:$firebaseVersion"
    implementation 'com.google.firebase:firebase-messaging:20.0.1'
    implementation 'com.google.firebase:firebase-config:19.0.3'
    implementation "com.crashlytics.sdk.android:crashlytics:$crashlyticsVersion"

    //koin injection
    implementation "org.koin:koin-android:$koinVersion"
    implementation "org.koin:koin-android-viewmodel:$koinVersion"

    implementation 'org.parceler:parceler-api:1.1.12'
    kapt 'org.parceler:parceler:1.1.12'

    //Room
    implementation "androidx.room:room-runtime:$roomVersion"
    kapt "androidx.room:room-compiler:$roomVersion"
    implementation "androidx.room:room-ktx:$roomVersion"
    implementation "androidx.room:room-rxjava2:$roomVersion"

    implementation 'com.sylversky.fontreplacer:fontreplacer:1.0'
    implementation 'com.yqritc:recyclerview-flexibledivider:1.4.0'

    implementation 'me.relex:circleindicator:2.1.4'

    implementation "com.hannesdorfmann:adapterdelegates4:$adapterDelegatesVersion"

    implementation "androidx.viewpager2:viewpager2:1.0.0-rc01"

    // Testing
    testImplementation 'junit:junit:4.12'
    androidTestImplementation 'androidx.test.ext:junit:1.1.1'
    androidTestImplementation 'androidx.test.espresso:espresso-core:3.2.0'
    androidTestImplementation "androidx.room:room-testing:$roomVersion"
}

sonarqube {
    properties {
        property 'sonar.projectName', 'Vote Monitor Android'
        property 'sonar.projectKey', 'code4romania_vote-monitor-android'
        property 'sonar.host.url', 'https://sonarcloud.io'
        property 'sonar.organization', 'code4romania'
    }
}

// TODO: Uncomment this to enable FirebaseAnalytics and Crashlytics
//apply plugin: 'com.google.gms.google-services'
